import planner_star.

main =>
  G = read_file_lines(),
  find_thing(G,'S',{Sx,Sy}),
  find_thing(G,'E',{Ex,Ey}),
  get_heap_map(grid).put(1,G),

  Dists = new_map(),
  Rev_dists = {new_map(), new_map(), new_map(), new_map()},

  % find cheapest costs from start to all reachable nodes (location + orientation tuples)
  dijkstra({Sx,Sy,e},0,successor,_,Dists,_),
  Best_cost = [Dists.get({Ex,Ey,D}) : D in [n,e,s,w], Dists.has_key({Ex,Ey,D})].min,
  println(dijkstra_best=Best_cost),
  % find cheapest costs from the end (in all 4 orientations) to every reachable node
  _ = [_ : {D,I} in zip([n,e,s,w],1..4), dijkstra({Ex,Ey,D},0,predecessor,_,Rev_dists[I],_)],
  % find all nodes on all shortest paths from start (facing east) to end (facing any orientation)
  Visited = [{X,Y} : {X,Y,D} in Dists.keys, Dists.get({X,Y,D}) = C, C <= Best_cost,
                     R in Rev_dists, R.has_key({X,Y,D}), R.get({X,Y,D}) + C = Best_cost].remove_dups,
  println(Visited.len).

find_thing(G,C,S0) ?=>
  nth(Y,G,Row),
  nth(X,Row,C),
  S0 = {X,Y}.

move(X,Y,n) = {X,Y-1,n}.
move(X,Y,e) = {X+1,Y,e}.
move(X,Y,s) = {X,Y+1,s}.
move(X,Y,w) = {X-1,Y,w}.
right(n) = e.
right(e) = s.
right(s) = w.
right(w) = n.
left(D) = D.right.right.right.

predecessor({X,Y,D},S1,Action,Cost) ?=>
  successor({X,Y,D.right.right},S2,Action,Cost),
  {Px,Py,Pd} = S2,
  S1 = {Px,Py,Pd.right.right}.

successor({Sx,Sy,D},S1,Action,Cost) ?=>
  (Action = f ; Action = l ; Action = r),
  {Lx,Ly,_} = move(Sx,Sy,D.left),
  {Rx,Ry,_} = move(Sx,Sy,D.right),
  G = get_heap_map(grid).get(1),
  if G[Ly,Lx] == '#' && G[Ry,Rx] == '#' then
    Action == f
  end,
  (
    (Action == f, {X,Y,D2} = move(Sx,Sy,D), Cost=1);
    (Action == r, {X,Y,D2} = {Sx,Sy,right(D)}, Cost=1000);
    (Action == l, {X,Y,D2} = {Sx,Sy,left(D)}, Cost=1000)
  ),
  nth(Y,G,Row),
  nth(X,Row,C),
  member(C,".ES"),
  S1 = {X,Y,D2}.
