% Didn't terminate within 13 hours on the full input data; even slower than the other version
import planner.
import ordset.

main =>
  G = read_file_lines(),
  show(G),
  find_thing(G,'S',{Sx,Sy}),
  find_thing(G,'E',{Ex,Ey}),
  get_heap_map(grid).put(1,G),

  garbage_collect(1000000000),
  V = new_ordset([{Sx,Sy}]),
  % find all valid plans with total cost determined in part 1 (or cheaper, but that's not possible)
  Plans = find_all(Plan, best_plan_nondet({Sx,Sy,Ex,Ey,e,V},65436,Plan,C)),
  M = new_set([{Sx,Sy}]),
  println(walking),
  foreach (Plan in Plans)
    walk(Plan,Sx,Sy,Ex,Ey,e,M)
  end,
  println(M.keys.len),
  nl.

show(G) =>
  foreach (L in G)
    println(L)
  end,
  nl.

find_thing(G,C,S0) ?=>
  nth(Y,G,Row),
  nth(X,Row,C),
  S0 = {X,Y}.

move(X,Y,n) = {X,Y-1,n}.
move(X,Y,e) = {X+1,Y,e}.
move(X,Y,s) = {X,Y+1,s}.
move(X,Y,w) = {X-1,Y,w}.
right(n) = e.
right(e) = s.
right(s) = w.
right(w) = n.
left(D) = D.right.right.right.

final({Sx,Sy,Ex,Ey,_,_}) => Sx = Ex, Sy = Ey, current_resource_plan_cost(_,_,C),C.println.

table
action({Sx,Sy,Ex,Ey,D,V}, S1, Action, Cost) ?=>
  (Action = f ; Action = l ; Action = r),
  G = get_heap_map(grid).get(1),
  {Lx,Ly,_} = move(Sx,Sy,D.left),
  {Rx,Ry,_} = move(Sx,Sy,D.right),
  if G[Ly,Lx] == '#' && G[Ry,Rx] == '#' then
    Action == f
  end,
  (
    (Action == f, {X,Y,D2} = move(Sx,Sy,D), Cost=1, not(membchk({X,Y},V)));
    (Action == r, {X,Y,D2} = {Sx,Sy,right(D)}, Cost=1000);
    (Action == l, {X,Y,D2} = {Sx,Sy,left(D)}, Cost=1000)
  ),
  nth(Y,G,Row),
  nth(X,Row,C),
  member(C,".ES"),
  S1 = {X,Y,Ex,Ey,D2,V.insert({X,Y})}.

heuristic({Sx,Sy,Ex,Ey,_,_}) = abs(Sx-Ex) + abs(Sy-Ey) + cond((Sx == Ex || Sy == Ey), 0, 1000).

walk([],Sx,Sy,Ex,Ey,D,M) => true.
walk([l|Plan],Sx,Sy,Ex,Ey,D,M) => walk(Plan,Sx,Sy,Ex,Ey,left(D),M).
walk([r|Plan],Sx,Sy,Ex,Ey,D,M) => walk(Plan,Sx,Sy,Ex,Ey,right(D),M).
walk([f|Plan],Sx,Sy,Ex,Ey,D,M) =>
  {X,Y,D2} = move(Sx,Sy,D),
  M.put({X,Y}),
  walk(Plan,X,Y,Ex,Ey,D2,M).
